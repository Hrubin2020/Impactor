buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

plugins {
    id 'com.qixalite.spongestart' version '1.6.2'
    id 'maven'
    id 'com.github.johnrengelman.shadow' version '1.2.3'
}

group 'com.nickimpact.impactor'
version '1.3.0'

apply plugin: 'net.minecraftforge.gradle.forge'

sourceCompatibility = targetCompatibility = "1.8" // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = "1.8"
}

ext {
    pluginversion = '1.3.0'
    spongeapiversion = '7.1.0-SNAPSHOT'
    completeversion = "-S${spongeapiversion.substring(0, 5)}-${pluginversion}"
}

jar {
    archiveName = "Impactor${completeversion}.jar"
}
repositories {
    mavenCentral()
    maven {
        name = 'sponge'
        url = 'http://repo.spongepowered.org/maven'
    }
    maven { url = 'https://jitpack.io' }
    maven {
        name 'drnaylor-minecraft'
        url 'http://repo.drnaylor.co.uk/artifactory/list/minecraft'
    }
    maven {
        name 'drnaylor'
        url 'http://repo.drnaylor.co.uk/artifactory/list/quickstart'
    }
    ivy {
        url "https://download.nodecdn.net/containers"
        layout "pattern", {
            artifact "/[organisation]/[module]/release/[revision]/[ext]"
        }
    }
}


minecraft {
    version = "1.12.2-14.23.1.2555"
    runDir = "run"
    mappings = "snapshot_20170919"
    useDepAts = true
}

spongestart{
    eula true

    //optional settings, takes latest version by default
    minecraft '1.12.2'
    type 'bleeding'
    spongeForgeVersion '1.12.2-2555-7.0.0-BETA-2774'
    spongeVanillaVersion '1.12.2-7.0.0-BETA-374'
}

dependencies {
    compile 'reforged:server:6.3.2@Pixelmon-1.12.2-6.3.2-server.jar'
    compile 'io.github.nucleuspowered:nucleus-api:1.4.5-SNAPSHOT-S7.0'
    compile 'org.spongepowered:spongeapi:7.1.0-SNAPSHOT'
    compile "org.projectlombok:lombok:1.16.10"
    compile group: 'com.zaxxer', name: 'HikariCP', version: '2.5.1'
    compile 'com.github.SparklingComet:java-mojang-api:-SNAPSHOT'
    compile 'com.github.rojo8399:PlaceholderAPI:master-SNAPSHOT'
    compile 'com.github.Sansko1337:NoteBlockAPI-Sponge:1.9.0-Sponge'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

//Fix forges AT breaking everything because the AT checker is stupid
[tasks.deobfMcMCP, tasks.deobfMcSRG].each { it.failOnAtError = false }

tasks.withType(JavaCompile) { task ->
    task.options.encoding = 'UTF-8'
    task.options.fork = true
}


processResources
{
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
