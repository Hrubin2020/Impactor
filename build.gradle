plugins {
    id 'com.qixalite.spongestart' version '1.6.2'
    id 'maven'
    id 'com.github.johnrengelman.shadow' version '4.0.4'
}

group 'com.nickimpact.impactor'
version '2.0.0-DEV'

sourceCompatibility = targetCompatibility = "1.8" // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = "1.8"
}

ext {
    spongeapiversion = '7.1.5'
    completeversion = "-S${spongeapiversion}-${project.version}"
}

jar {
    archiveName = "Impactor${completeversion}.jar"
}

repositories {
    mavenCentral()
    maven {
        name = 'sponge'
        url = 'http://repo.spongepowered.org/maven'
    }
    maven { url = 'https://jitpack.io' }
    maven {
        name 'drnaylor-minecraft'
        url 'http://repo.drnaylor.co.uk/artifactory/list/minecraft'
    }
    maven {
        name 'drnaylor'
        url 'http://repo.drnaylor.co.uk/artifactory/list/quickstart'
    }
}

spongestart{
    eula true

    //optional settings, takes latest version by default
    minecraft '1.12.2'
}

dependencies {
    compile fileTree(dir: '/libs', include: '*.jar')
    compile 'io.github.nucleuspowered:nucleus-api:1.7.0-S7.1'
    compile 'org.spongepowered:spongeapi:7.1.0'
    compile "org.projectlombok:lombok:1.18.6"
    compile group: 'com.zaxxer', name: 'HikariCP', version: '2.5.1'
    compile 'com.github.SparklingComet:java-mojang-api:60d6231d42'
    compile 'com.github.rojo8399:PlaceholderAPI:master-SNAPSHOT'
    compile 'com.github.Sansko1337:NoteBlockAPI-Sponge:1.9.0-Sponge'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

tasks.withType(JavaCompile) { task ->
    task.options.encoding = 'UTF-8'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'com.github.johnrengelman.shadow'

    group = 'com.nickimpact.impactor'
    version = '2.0.0-DEV'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    project.ext.sponge = 'S7.1'
    project.ext.spigot = '1.12.2'

    repositories {
        mavenCentral()
        maven { url = 'https://jitpack.io' }
    }

    dependencies {
        shadow "org.projectlombok:lombok:1.18.4"
        shadow 'com.google.guava:guava:27.1-jre'
        shadow 'com.google.code.gson:gson:2.8.5'
        testCompile group: 'junit', name: 'junit', version: '4.12'
    }
}

task copyJars(type: Copy) {
    from subprojects.collect { it.tasks.withType(Jar) }
    into "$buildDir/allJars"
    exclude '*-all.jar'
    exclude '*-sources.jar'
    exclude 'api*.jar'
    exclude 'common*.jar'
    exclude 'spigot*.jar'
    exclude 'sponge*.jar'
    exclude '*API.jar'
    exclude '*Common.jar'
}

build.dependsOn copyJars